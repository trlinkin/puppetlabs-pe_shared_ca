# Shared Certificates for Puppet Masters
#
# This class is intended to be run in a oneoff scenario to aid in the
# bootstrapping of a shared ca environment.  It is not meant to be
# permanantly installed as part of a maintained Puppet environment.
#
class pe_shared_ca (
  $ca_server,
  $manage_puppet_conf  = true,
  $puppet_user         = $pe_shared_ca::params::puppet_user,
  $puppet_group        = $pe_shared_ca::params::puppet_group,
  $services            = $pe_shared_ca::params::services,
  $source_uri          = "puppet:///modules/${module_name}/ssl",
  $mco_credentials_uri = "puppet:///modules/${module_name}/credentials",
) inherits pe_shared_ca::params {
  validate_bool($ca_server)

  ## Stop services before purging cert files
  service { $services:
    ensure  => 'stopped',
    before  => File[$mco_files_to_purge, $ca_files_to_purge],
  }

  ## Purge old ssl files
  file { $mco_files_to_purge:
    ensure  => absent,
    recurse => true,
    force   => true,
  }
  file { $ca_files_to_purge:
    ensure  => absent,
    recurse => true,
    force   => true,
  }

  if $ca_server {
    ## Update CA directory and remove all pre-existing files
    file { "${ssldir}/ca":
      ensure  => directory,
      owner   => $puppet_user,
      group   => $puppet_group,
      source  => "${source_uri}/ca",
      recurse => true,
      purge   => true,
      force   => true,
      require => File[$files_to_purge],
    }
    if $manage_puppet_conf {
      ini_setting { 'master ca setting':
        path    => '/etc/puppetlabs/puppet/puppet.conf',
        section => 'master',
        setting => 'ca',
        value   => 'true',
      }
    }
  } else {
    ## Remove CA directory from non-ca-server
    file { "${ssldir}/ca":
      ensure  => absent,
      recurse => true,
      force   => true,
    }
    if $manage_puppet_conf {
      ini_setting { 'master ca setting':
        path    => '/etc/puppetlabs/puppet/puppet.conf',
        section => 'master',
        setting => 'ca',
        value   => 'false',
      }
    }
  }

  ## Update pe-internal certs
  file { "${ssldir}/certs":
    ensure  => directory,
    owner   => $puppet_user,
    group   => $puppet_group,
    source  => "${source_uri}/certs",
    recurse => true,
  }
  ## Update pe-internal private_keys
  file { "${ssldir}/private_keys":
    ensure  => directory,
    owner   => $puppet_user,
    group   => $puppet_group,
    mode    => '0640',
    source  => "${source_uri}/private_keys",
    recurse => true,
  }
  ## Update pe-internal public_keys
  file { "${ssldir}/public_keys":
    ensure  => directory,
    owner   => $puppet_user,
    group   => $puppet_group,
    source  => "${source_uri}/public_keys",
    recurse => true,
  }
  ## Update MCO credentials file
  file { "/etc/puppetlabs/mcollective/credentials":
    ensure => file,
    owner  => $puppet_user,
    group  => $puppet_group,
    source => $mco_credentials_uri,
    mode   => '0600',
    require => File[$mco_files_to_purge, $ca_files_to_purge],
  }
}
